/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.2/userguide/tutorial_java_projects.html
 */

plugins {
    id 'jacoco'
    id 'java'
    id 'maven-publish'
    id 'io.freefair.lombok' version '6.6.2'
    id 'org.sonarqube' version '2.8'
    id 'com.palantir.git-version' version '0.12.3'
    id 'com.diffplug.spotless' version '6.7.1'
}

// Apply the java plugin to add support for Java
apply plugin: 'java'

version gitVersion()
sourceCompatibility = 1.8
targetCompatibility = 1.8

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
               TestLogEvent.PASSED,
               TestLogEvent.SKIPPED,
               TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                   TestLogEvent.FAILED,
                   TestLogEvent.PASSED,
                   TestLogEvent.SKIPPED,
                   TestLogEvent.STANDARD_ERROR,
                   TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.19.0'
}

test {
    useJUnitPlatform()
}


jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
  reports {
    xml {
        required = true
    }
  }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'id.koneko096'
            artifactId = 'classy-core'
            version = project.version
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Classy'
                description = 'Simple kNN and NaiveBayes classifier implementation'
                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "koneko096"
                        name = "Afrizal Fikri"
                        email = "afrizalf96@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/koneko096/Classy.git"
                    developerConnection = "scm:git:ssh://github.com/koneko096/Classy.git"
                    url = "https://github.com/koneko096/Classy"
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/koneko096/Classy"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

spotless {
    // optional: limit format enforcement to just the files changed by this feature branch
    ratchetFrom 'origin/master'

    java {
        removeUnusedImports()
        importOrder()
        googleJavaFormat('1.15.0').reflowLongStrings()

    }
}

sonarqube {
  properties {
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.organization', 'koneko096-github'
    property 'sonar.projectKey', 'koneko096_Classy'
    property 'sonar.exclusions', 'src/main/java/id/koneko096/Classy/Data/**/*,src/main/java/id/koneko096/Classy/Util/**/*'
  }
}
